plugins {
  id "war"
  id "idea"
  id "maven-publish"
  id "org.springframework.boot" version "2.5.6"
  id "io.spring.dependency-management" version "1.0.11.RELEASE"
  id "org.jetbrains.kotlin.jvm" version "1.5.20"
  id "org.jetbrains.kotlin.plugin.spring" version "1.5.20"
}

compileKotlin {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11

  kotlinOptions {
    apiVersion = "1.4"
    languageVersion = "1.4"
    jvmTarget = "11"
  }
}


def serviceVersion = project.hasProperty('verifierServiceProjectVersion') ? project.verifierServiceProjectVersion : '1.0'

configure(allprojects) {
  version = serviceVersion
  group = "org.jetbrains.intellij.plugins.verifier"

  idea {
    module {
      inheritOutputDirs = false
      outputDir = file("$buildDir/classes/main")
    }
  }

  bootRun {
    systemProperties System.properties
  }

  springBoot {
    buildInfo()
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies" }
    maven { url "https://packages.jetbrains.team/maven/p/teamcity-rest-client/teamcity-rest-client" }
  }

  configurations {
    developmentOnly
    runtimeClasspath {
      extendsFrom developmentOnly
    }
  }

  dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web") {
      dependencies {
        implementation("org.apache.logging.log4j:log4j-to-slf4j:2.17.0") {
          because("we need 2.17.0")
        }
        implementation("org.apache.logging.log4j:log4j-api:2.17.0") {
          because("we need 2.17.0")
        }
      }
    }
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation 'junit:junit:4.13.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation group: 'commons-io', name: 'commons-io', version: '2.10.0'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
    implementation 'com.google.code.gson:gson:2.8.8'

    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation('com.squareup.retrofit2:retrofit:2.9.0')
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    //Simple map-database engine that allows to store maps on disk: https://github.com/jankotek/mapdb/
    implementation 'org.mapdb:mapdb:3.0.8'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.31'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.6'
    runtimeOnly "org.codehaus.groovy:groovy:2.4.12"

    implementation "commons-fileupload:commons-fileupload:1.4"

    implementation group: 'org.jetbrains.intellij.plugins', name: 'intellij-feature-extractor', version: 'dev'
    implementation group: 'org.jetbrains.intellij.plugins', name: 'verifier-intellij', version: 'dev'

    implementation 'org.jetbrains.teamcity:teamcity-rest-client:1.14.0'
  }

}

publishToMavenLocal.dependsOn test
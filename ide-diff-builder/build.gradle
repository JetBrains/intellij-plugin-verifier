plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "org.jetbrains.kotlin.jvm" version "1.3.70"
  id "org.jetbrains.kotlin.plugin.serialization" version "1.3.70" apply false
}

ext.kotlin_version = '1.3.70'

allprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
    maven { url "https://dl.bintray.com/jetbrains/intellij-plugin-service" }
  }
}

jar {
  manifest {
    attributes(
        'Main-Class': 'org.jetbrains.ide.diff.builder.MainKt'
    )
  }
}

task fatJar(type: Jar) {
  manifest.from jar.manifest
  classifier = 'all'
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  } {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
  }
  with jar
}

artifacts {
  fatJar
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs = ["-Xjvm-default=enable"]
  }
}

dependencies {
  runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

  testImplementation 'junit:junit:4.12'
  testImplementation project(':')
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  implementation group: 'org.jetbrains.intellij.plugins', name: 'verifier-intellij', version: 'dev'
  implementation group: 'org.jetbrains.intellij.plugins', name: 'verifier-cli', version: 'dev'
  implementation group: 'org.jetbrains.intellij.plugins', name: 'structure-ide-classes', version: 'dev'
  implementation 'com.github.spullara.cli-parser:cli-parser:1.1.2'
  implementation 'org.apache.commons:commons-text:1.6'
  implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0'
}

task copyMockIdes(type: Copy, dependsOn: [':mock-old-ide:prepareIde', ':mock-new-ide:prepareIde']) {
  into "$buildDir/mock-ides"
  def oldIde = copySpec {
    from new File(project('mock-old-ide').buildDir, "mock-ide")
    into "old-ide"
  }
  def newIde = copySpec {
    from new File(project('mock-new-ide').buildDir, "mock-ide")
    into "new-ide"
  }
  with oldIde, newIde
}

test.dependsOn(copyMockIdes, copyMockIdes.outputs.files)
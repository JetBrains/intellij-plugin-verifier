def separator = File.separator
def mocksPath = "$buildDir" + separator + "mocks"
def mocksDir = new File(mocksPath)

version = '1.0'

//Compile library represents jars that could be packed into <plugin>/lib/<some-directory>/ (relative to /lib)
//Such jars are added to external build processes class-path (via 'classpath' attribute of 'compileServer.plugin' extension point)
//See PR-1063 and com.intellij.compiler.server.CompileServerPlugin for details.
def compileLibraryJarTask = project(':tests:mock-compile-library').getTasks().findByName('jar')

task prepareMockAsJar(type: Copy, dependsOn: jar) {
  //creates mock-plugin-1.0.jar file
  into mocksPath
  from jar
}

task prepareInvalidMockJarAsZip(type: Zip, dependsOn: prepareMockAsJar) {
  //creates plugin distribution with invalid structure: classes are located in the root of the zip
  from zipTree(mocksPath + separator + "mock-plugin-1.0.jar")
  destinationDir = mocksDir
  archiveName = "invalid-mock-pluginJarAsZip.zip"
}

task prepareMockAsDir(type: Copy, dependsOn: [jar, compileLibraryJarTask]) {
  //create mock-plugin-dir/ which contains /lib/mock-plugin.jar inside
  def copyJar = copySpec {
    from jar
    into "lib"
  }

  def copyCompileLib = copySpec {
    from compileLibraryJarTask
    into "lib" + separator + "compile"
  }

  into mocksPath + separator + "mock-plugin-dir"

  with copyJar, copyCompileLib
}

task prepareZipLibMock(type: Zip, dependsOn: [jar, compileLibraryJarTask]) {
  //create mock-plugin-lib.zip which contains /lib/mock-plugin.jar inside

  def copyJarToLib = copySpec {
    from jar
    into "lib"
  }

  def copyCompileLib = copySpec {
    from compileLibraryJarTask
    into "lib" + separator + "compile"
  }

  with copyJarToLib, copyCompileLib
  destinationDir = mocksDir
  archiveName = "mock-plugin-lib.zip"
}

task prepareJarInZip(type: Zip, dependsOn: jar) {
  //create mock-plugin-jar-in-zip.zip which contains /mock-plugin.jar in the root
  from jar
  //to the root
//  into "."
  destinationDir = mocksDir
  archiveName = "mock-plugin-jar-in-zip.zip"
}

task prepareMockClasses(type: Copy, dependsOn: [classes, compileLibraryJarTask]) {
  //create mock-plugin-classes/ with /classes/ dir and /META-INF/ dir inside
  into mocksPath + separator + "mock-plugin-classes"

  def classesSpec = copySpec {
    from sourceSets.main.output.classesDir
    into "classes"
  }

  def pluginXmlSpec = copySpec {
    from sourceSets.main.output.resourcesDir
    into "."
  }

  def copyCompileLib = copySpec {
    from compileLibraryJarTask
    into "lib" + separator + "compile"
  }

  with classesSpec, pluginXmlSpec, copyCompileLib
}

task prepareZipMockClassesAsZip(type: Zip, dependsOn: prepareMockClasses) {
  //create mock-plugin-classes.zip/ which is equivalent to prepareMockClasses but in the .zip
  from mocksPath + separator + "mock-plugin-classes" + separator
  into "mock-plugin"
  destinationDir = mocksDir
  archiveName = "mock-plugin-classes-zip.zip"
}

task prepareMockDirectoryWithLibInZip(type: Zip, dependsOn: prepareMockAsDir) {
  from prepareMockAsDir
  into "mock-plugin"
  archiveName = "mock-plugin-directory-with-lib-in-zip.zip"
  destinationDir = mocksDir
}

task prepareAllMocks(dependsOn: [prepareMockAsJar, prepareInvalidMockJarAsZip, prepareMockAsDir, prepareZipLibMock, prepareJarInZip, prepareMockClasses, prepareZipMockClassesAsZip, prepareMockDirectoryWithLibInZip])
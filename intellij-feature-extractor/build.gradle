plugins {
  id "org.jetbrains.kotlin.jvm" version "1.5.20"
}

ext.kotlin_version = '1.5.20'

def extractorVersion = project.hasProperty('featureExtractorVersion') ? project.featureExtractorVersion : 'dev'

configure(allprojects) {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'maven-publish'

  version = extractorVersion
  group = 'org.jetbrains.intellij.plugins'

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  }

  sourceCompatibility = 11
  targetCompatibility = 11

  repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies" }
  }
}

dependencies {
  def structureVersion = "dev"
  implementation "org.jetbrains.intellij.plugins:structure-intellij-classes:$structureVersion"
  implementation "org.jetbrains.intellij.plugins:structure-ide-classes:$structureVersion"
  implementation "org.jetbrains.intellij.plugins:verifier-core:$structureVersion"

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.8'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
  implementation group: 'commons-io', name: 'commons-io', version: '2.5'

  testImplementation group: 'junit', name: 'junit', version: '4.13.2'
  testImplementation project(':test-classes')
}

task sourcesJar(type: org.gradle.api.tasks.bundling.Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts.archives sourcesJar

publishing {

  publications {

    def configurePublication = { org.gradle.api.publish.maven.MavenPublication pub ->

      pub.groupId group
      pub.artifactId name
      pub.version version

      pub.from components.java
      artifacts
      pub.artifact sourcesJar
    }

    ProjectPublication(org.gradle.api.publish.maven.MavenPublication) { configurePublication(it) }
  }
}

publishToMavenLocal.dependsOn test
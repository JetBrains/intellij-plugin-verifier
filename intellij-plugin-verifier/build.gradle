plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "org.jetbrains.kotlin.jvm" version "1.3.30"
  id "maven-publish"
}

ext.kotlin_version = '1.3.30'

def projectVersion = project.hasProperty('verifierProjectVersion') ? project.verifierProjectVersion : 'dev'
def isDevMode = projectVersion == 'dev'
def intellijStructureVersion = 'dev'
if (!isDevMode) {
  if (project.hasProperty('intellijStructureVersion')) {
    intellijStructureVersion = project.intellijStructureVersion
  } else {
    throw new MissingPropertyException("Version of intellij-structure library to be used is not specified via 'intellijStructureVersion'")
  }
}

project.ext.intellijStructureVersion = intellijStructureVersion

configure(allprojects) {
  apply plugin: 'java'
  apply plugin: 'kotlin'

  group = 'org.jetbrains.intellij.plugins'

  version = projectVersion

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
      jvmTarget = "1.8"
      freeCompilerArgs = ["-Xjvm-default=enable"]
    }
  }

  dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.salomonbrys.kotson:kotson:2.4.0'

    implementation "org.jetbrains.intellij.plugins:structure-intellij:$intellijStructureVersion"

    testImplementation group: 'junit', name: 'junit', version: '4.11'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.61'

    implementation group: 'com.intellij', name: 'annotations', version: '12.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.7'
  }

  repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://dl.bintray.com/jetbrains/intellij-plugin-service" }
    maven { url "https://dl.bintray.com/sergey-patrikeev/patrikeev-archiver"  }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
  }
}

configure(childProjects.values()) {

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts.archives sourcesJar

  jar {
    manifest {
      attributes 'Verifier-Version': version
    }
  }

}

publishing {

  publications {

    def configurePublication = { MavenPublication pub, String projectName ->

      def proj = project(projectName)
      pub.groupId proj.group
      pub.artifactId proj.name
      pub.version proj.version

      pub.from proj.components.java
      proj.artifacts
      pub.artifact proj.sourcesJar
      if (proj.hasProperty("shadowJar")) {
        pub.artifact proj.shadowJar
      }
    }

    VerifierCliPublication(MavenPublication) { configurePublication(it, ':verifier-cli') }
    VerifierCorePublication(MavenPublication) { configurePublication(it, ':verifier-core') }
    VerifierIntelliJPublication(MavenPublication) { configurePublication(it, ':verifier-intellij') }
    RepositoryPublication(MavenPublication) { configurePublication(it, ':verifier-repository') }
  }
}

test.dependsOn project(':verifier-test').test
publishToMavenLocal.dependsOn test


bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')


  publish = true
  publications = ['VerifierCliPublication', 'VerifierCorePublication', 'VerifierIntelliJPublication', 'RepositoryPublication']

  pkg {
    repo = 'intellij-plugin-service'
    name = 'intellij-plugin-verifier'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/JetBrains/intellij-plugin-verifier'
    userOrg = 'jetbrains'

    version {
      name = project.version
    }
  }
}

bintrayUpload.doFirst {

  if (!bintray.user || !bintray.key) {
    throw new StopExecutionException("You must specify (bintrayUser, bintrayApiKey) or (BINTRAY_USER, BINTRAY_API_KEY) properties")
  }

  println("Uploading Plugin Verifier module ${project.name}:${project.version}" + ' to bintray')
}

//run tests before deploying
//run tests before deploying
bintrayUpload.dependsOn test

plugins {
  id 'com.github.johnrengelman.shadow' version '7.0.0'
}

dependencies {
  api project(':verifier-intellij')

  runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.7'
  implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.5"
  implementation 'com.github.spullara.cli-parser:cli-parser:1.1.5'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
}

task versionTxt() {
  def versionTxt = new File(buildDir, "intellij-plugin-verifier-version.txt")
  outputs.file(versionTxt)
  doLast {
    versionTxt.text = projectVersion
  }
}

jar {
  metaInf {
    from(versionTxt)
  }
}

shadowJar {
  metaInf {
    from(versionTxt)
  }
  manifest {
    attributes 'Main-Class': 'com.jetbrains.pluginverifier.PluginVerifierMain'
  }

  archiveClassifier = 'all'

  //Exclude resources/dlls and other stuff coming from the dependencies.
  exclude([
      '/win32/**',
      '/tips/**',
      '/search/**',
      '/linux/**',
      '/intentionDescriptions/**',
      '/inspectionDescriptions/**',
      '/fileTemplates/**',
      '/darwin/**',
      '**.dll'
  ])
}

jar.finalizedBy(shadowJar)